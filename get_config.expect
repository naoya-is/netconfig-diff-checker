#!/usr/bin/expect -f

set timeout 20
set host [lindex $argv 0]

# --- ssh -G で User を取得 ---
set getuser [open "|ssh -G $host | grep {^user } | awk '{print \$2}'" r]
set user [string trim [gets $getuser]]
close $getuser

if {$user eq ""} {
    send_user "エラー: ssh -G でホスト '$host' のユーザー名を取得できません\n"
    exit 1
}

# --- 対話式でパスワードを入力 ---
stty -echo
send_user "SSH Password: "
expect_user -re "(.*)\n"
set pass $expect_out(1,string)

send_user "Enable Password: "
expect_user -re "(.*)\n"
set enablepass $expect_out(1,string)
stty echo
send_user "\n"

# 出力先
set outfile "/tmp/${host}_running-config.tmp"

spawn ssh $user@$host
expect {
    "yes/no" { send "yes\r"; exp_continue }
    "Password:" { send "$pass\r" }
}

expect ">"
send "enable\r"
expect "Password:"
send "$enablepass\r"

expect "#"
send "terminal length 0\r"
expect "#"
send "show running-config\r"

log_file -noappend $outfile
expect "#"
log_file

send "exit\r"
expect eof
